apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'com.ymcmp.rset.Main'

run.standardInput = System.in

repositories {
    jcenter()
}

dependencies {
    compile 'org.ow2.asm:asm:6.2'
    testCompile 'junit:junit:4.12'
}

task copyLicense {
    outputs.file new File("$buildDir/LICENSE")
    outputs.file new File("$buildDir/COPYING")
    doLast {
        copy {
            from "LICENSE"
            into "$buildDir"
        }
        copy {
            from "COPYING"
            into "$buildDir"
        }
    }
}

applicationDistribution.from(copyLicense) {
    into ""
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}